# Copyright 2018 the Deno authors. All rights reserved. MIT license.
import("//build/toolchain/cc_wrapper.gni")
import("//third_party/v8/gni/v8.gni")
import("//third_party/v8/snapshot_toolchain.gni")
import("//build_extra/flatbuffers/flatbuffer.gni")
import("//build_extra/flatbuffers/rust/rust_flatbuffer.gni")
import("//build_extra/deno.gni")
import("//build_extra/rust/rust.gni")
import("//build_extra/toolchain/validate.gni")

group("default") {
  testonly = true
  deps = [
    ":deno",
    ":hyper_hello",
    ":test_cc",
    ":test_rs",
  ]
}

# Set of targets that need to be built for `cargo check` to succeed.
group("cargo_check_deps") {
  deps = [
    ":msg_rs",
  ]
}

config("deno_config") {
  include_dirs = [ "third_party/v8" ]  # This allows us to v8/src/base/ libraries.
  configs = [ "third_party/v8:external_config" ]
  if (is_debug) {
    defines = [ "DEBUG" ]
  }

  # Targets built with the `rust_executable()` template automatically pick up
  # these dependencies, but those built with `executable()` need them when they
  # have Rust inputs. Currently, there's only one such target, `test_cc`.
  if (is_mac) {
    libs = [ "resolv" ]
  }
  if (is_win) {
    libs = [ "userenv.lib" ]
  }

  if (is_clang) {
    cflags = [
      "-fcolor-diagnostics",
      "-fansi-escape-codes",
    ]
  }
}

main_extern = [
  "$rust_build:atty",
  "$rust_build:dirs",
  "$rust_build:futures",
  "$rust_build:getopts",
  "$rust_build:hyper",
  "$rust_build:hyper_rustls",
  "$rust_build:lazy_static",
  "$rust_build:libc",
  "$rust_build:log",
  "$rust_build:rand",
  "$rust_build:remove_dir_all",
  "$rust_build:ring",
  "$rust_build:rustyline",
  "$rust_build:tempfile",
  "$rust_build:tokio",
  "$rust_build:tokio_executor",
  "$rust_build:tokio_fs",
  "$rust_build:tokio_io",
  "$rust_build:tokio_process",
  "$rust_build:tokio_threadpool",
  "$rust_build:url",
  "//build_extra/flatbuffers/rust:flatbuffers",
]

ts_sources = [
  "js/assets.ts",
  "js/blob.ts",
  "js/buffer.ts",
  "js/chmod.ts",
  "js/compiler.ts",
  "js/console.ts",
  "js/copy_file.ts",
  "js/deno.ts",
  "js/dir.ts",
  "js/dispatch.ts",
  "js/dom_types.ts",
  "js/errors.ts",
  "js/fetch.ts",
  "js/file.ts",
  "js/headers.ts",
  "js/file_info.ts",
  "js/files.ts",
  "js/flatbuffers.ts",
  "js/form_data.ts",
  "js/global_eval.ts",
  "js/globals.ts",
  "js/io.ts",
  "js/libdeno.ts",
  "js/main.ts",
  "js/make_temp_dir.ts",
  "js/metrics.ts",
  "js/mkdir.ts",
  "js/mock_builtin.js",
  "js/net.ts",
  "js/os.ts",
  "js/platform.ts",
  "js/plugins.d.ts",
  "js/promise_util.ts",
  "js/read_dir.ts",
  "js/read_file.ts",
  "js/read_link.ts",
  "js/remove.ts",
  "js/rename.ts",
  "js/resources.ts",
  "js/repl.ts",
  "js/stat.ts",
  "js/symlink.ts",
  "js/text_encoding.ts",
  "js/timers.ts",
  "js/truncate.ts",
  "js/types.ts",
  "js/url_search_params.ts",
  "js/util.ts",
  "js/v8_source_maps.ts",
  "js/write_file.ts",
  "tsconfig.json",

  # Listing package.json and yarn.lock as sources ensures the bundle is rebuilt
  # when npm packages are added/removed or their contents changes.
  "package.json",
  "third_party/yarn.lock",
]

rust_executable("deno") {
  source_root = "src/main.rs"
  extern = main_extern
  deps = [
    ":deno_deps",
  ]
}

source_set("snapshot") {
  inputs = [
    "$target_gen_dir/snapshot_deno.bin",
  ]
  deps = [
    ":create_snapshot_deno",
  ]
}

rust_executable("hyper_hello") {
  source_root = "tools/hyper_hello.rs"
  extern = [
    "$rust_build:hyper",
    "$rust_build:ring",
  ]
}

rust_test("test_rs") {
  source_root = "src/main.rs"
  extern = main_extern
  deps = [
    ":deno_deps",
  ]
}

v8_executable("test_cc") {
  testonly = true
  sources = [
    "libdeno/file_util_test.cc",
    "libdeno/libdeno_test.cc",
    "libdeno/test.cc",
  ]
  deps = [
    ":create_snapshot_libdeno_test",
    ":libdeno",
    "//testing/gtest:gtest",
  ]
  data = [
    "$target_gen_dir/snapshot_libdeno_test.bin",
  ]
  snapshot_path = rebase_path(data[0], root_build_dir)
  defines = [ "SNAPSHOT_PATH=\"$snapshot_path\"" ]
  configs = [ ":deno_config" ]
}

# Only functionality needed for libdeno_test and snapshot_creator
# In particular no flatbuffers, no assets, no rust, no msg handlers.
# Because snapshots are slow, it's important that snapshot_creator's
# dependencies are minimal.
static_library("libdeno") {
  sources = [
    "libdeno/api.cc",
    "libdeno/binding.cc",
    "libdeno/deno.h",
    "libdeno/file_util.cc",
    "libdeno/file_util.h",
    "libdeno/internal.h",
  ]
  public_deps = [
    "third_party/v8:v8_monolith",
  ]
  configs += [ ":deno_config" ]
}

static_library("deno_deps") {
  complete_static_lib = true
  public_deps = [
    ":libdeno",
    ":msg_rs",
    ":snapshot",
  ]
  configs += [ ":deno_config" ]
}

executable("snapshot_creator") {
  sources = [
    "libdeno/snapshot_creator.cc",
  ]
  deps = [
    ":libdeno",
  ]
  configs += [ ":deno_config" ]
}

# Generates the core TypeScript type library for deno that will be
# included in the runtime bundle
run_node("deno_runtime_declaration") {
  out_dir = target_gen_dir
  sources = ts_sources
  outputs = [
    "$out_dir/lib/lib.deno_runtime.d.ts",
  ]
  deps = [
    ":msg_ts",
  ]
  args = [
    rebase_path("node_modules/ts-node/dist/bin.js", root_build_dir),
    "--project",
    rebase_path("tools/ts_library_builder/tsconfig.json"),
    rebase_path("tools/ts_library_builder/main.ts", root_build_dir),
    "--basePath",
    rebase_path(".", root_build_dir),
    "--buildPath",
    rebase_path(root_build_dir, root_build_dir),
    "--outFile",
    rebase_path("$out_dir/lib/lib.deno_runtime.d.ts", root_build_dir),
    "--silent",
  ]
  if (is_debug) {
    args += [ "--debug" ]
  }
}

run_node("bundle") {
  out_dir = "$target_gen_dir/bundle/"
  outputs = [
    out_dir + "main.js",
    out_dir + "main.js.map",
  ]
  depfile = out_dir + "main.d"
  deps = [
    ":deno_runtime_declaration",
    ":msg_ts",
  ]
  args = [
    rebase_path("third_party/node_modules/rollup/bin/rollup", root_build_dir),
    "-c",
    rebase_path("rollup.config.js", root_build_dir),
    "-i",
    rebase_path("js/main.ts", root_build_dir),
    "-o",
    rebase_path(out_dir + "main.js", root_build_dir),
    "--sourcemapFile",
    rebase_path("."),
    "--silent",
  ]
}

action("bundle_hash_h") {
  script = "//tools/sha256sum.py"
  inputs = get_target_outputs(":bundle")
  outputs = [
    "$target_gen_dir/bundle/hash.h",
  ]
  deps = [
    ":bundle",
  ]
  args = [
    "--format",
    "__attribute__((__unused__)) static const int dummy_%s = 0;",
    "--outfile",
    rebase_path(outputs[0], root_build_dir),
  ]
  foreach(input, inputs) {
    args += [
      "--infile",
      rebase_path(input, root_build_dir),
    ]
  }
}

ts_flatbuffer("msg_ts") {
  sources = [
    "src/msg.fbs",
  ]
}

rust_flatbuffer("msg_rs") {
  sources = [
    "src/msg.fbs",
  ]
}

# Generates $target_gen_dir/snapshot_deno.bin
create_snapshot("deno") {
  js = "$target_gen_dir/bundle/main.js"
  source_map = "$target_gen_dir/bundle/main.js.map"
  deps = [
    ":bundle",
  ]
}

# Generates $target_gen_dir/snapshot_libdeno_test.bin
create_snapshot("libdeno_test") {
  testonly = true
  js = "libdeno/libdeno_test.js"
}
